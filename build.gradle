plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "7.2.2.6593"
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories { mavenCentral() }

ext {
	set('springCloudVersion', "2025.1.1")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

/* ---------------- Integration test source set + configurations ---------------- */
sourceSets {
	integrationTest {
		java.srcDirs = ['src/integrationTest/java']
		resources.srcDirs = ['src/integrationTest/resources']

		compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
		runtimeClasspath += output + compileClasspath
	}
}

// IMPORTANT: define configs before using them in `dependencies { }`
configurations {
	integrationTestImplementation
	integrationTestRuntimeOnly

	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}
/* --------------------------------------------------------------------------- */

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Postgres for the real app runtime
	runtimeOnly 'org.postgresql:postgresql'

	// Swagger / OpenAPI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

	implementation 'io.github.cdimascio:dotenv-java:3.0.0'

	// Feign
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	testImplementation "io.github.openfeign:feign-jackson:13.5"

	// Unit tests (H2)
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'

	// WireMock (OK for unit + integration tests)
	testImplementation "org.wiremock:wiremock-standalone:3.13.2"

	// MapStruct
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	// Integration tests (Testcontainers + Postgres driver)
	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationTestImplementation 'org.testcontainers:junit-jupiter:1.20.3'
	integrationTestImplementation 'org.testcontainers:postgresql:1.20.3'
	integrationTestRuntimeOnly 'org.postgresql:postgresql'
}

test {
	useJUnitPlatform()

	// fixes mockito dynamic agent loading warning on JDK 25
	// safe configuration and recommended by mockito documentation
	jvmArgs "-XX:+EnableDynamicAgentLoading"

	finalizedBy jacocoTestReport
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	useJUnitPlatform()

	// fixes mockito dynamic agent loading warning on JDK 25
	// required for testcontainers and mockito on newer JDK versions
	jvmArgs "-XX:+EnableDynamicAgentLoading"

	testLogging {
		events "failed", "skipped"
		exceptionFormat "full"
		showStandardStreams true
	}
}

check.dependsOn tasks.integrationTest

jacoco {
	toolVersion = "0.8.13"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "danevairena_stock-exchange-market-companies"
		property "sonar.organization", "danevairena"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
	}
}